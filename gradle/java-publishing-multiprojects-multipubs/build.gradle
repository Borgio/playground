//
//
//

group = 'io.example.blue'
version = '1.0'

def apiArtifactBasename = "blue-api"
def runtimeArtifactBasename = "blue-rt"


apply plugin: 'maven-publish'

subprojects {

    ext {

        isApi = project.name.contains("api")
    }

    apply plugin: 'java'

    task copySources(type: Copy) {

        from 'src/main/java'
        into parent.buildDir.path + (project.isApi ? "/api-sources" : "/rt-sources")

        doLast {

            println "copied from src/main/java to " + parent.buildDir.path + (project.isApi ? "/api-sources" : "/rt-sources")
        }
    }

    task copyClasses(type: Copy) {

        //
        // we need to build to get classes
        //

        dependsOn 'build'

        from 'build/classes/java/main'
        into parent.buildDir.path + (project.isApi ? "/api-classes" : "/rt-classes")

        doLast {

            println "copied from src/main/java to " + parent.buildDir.path + (project.isApi ? "/api-sources" : "/rt-sources")
        }
    }

    task copyArtifacts {

        dependsOn 'copySources', 'copyClasses'
    }

}

task apiSources(type: Jar) {

    destinationDir = buildDir
    baseName = apiArtifactBasename
    classifier = 'sources'
    from buildDir.path + "/api-sources"
}

task apiClasses(type: Jar) {

    destinationDir = buildDir
    baseName = apiArtifactBasename
    from buildDir.path + "/api-classes"
}

task runtimeSources(type: Jar) {

    destinationDir = buildDir
    baseName = runtimeArtifactBasename
    classifier = 'sources'
    from buildDir.path + "/rt-sources"
}

task runtimeClasses(type: Jar) {

    destinationDir = buildDir
    baseName = runtimeArtifactBasename
    from buildDir.path + "/rt-classes"
}

publishing {

    publications {

        api(MavenPublication) {

            artifactId apiArtifactBasename
            artifact apiSources
            artifact apiClasses
        }

        runtime(MavenPublication) {

            artifactId runtimeArtifactBasename
            artifact runtimeSources
            artifact runtimeClasses
        }
    }

    repositories {

        mavenLocal()
    }
}

task clean(type: Delete) {

    delete 'build'
}